plugins {
    id "java"
    id "org.springframework.boot" version "3.5.8"
    id "io.spring.dependency-management" version "1.1.7"
    id "com.google.cloud.tools.jib" version "3.5.1"
    id "com.diffplug.spotless" version "8.1.0"
}

group = "me.jaeyeop"
version = "1.0.0"

// Pin commons-lang3 version managed by spring boot dependency management
// (Security vulnerability in versions 3.17.0 and below)
ext['commons-lang3.version'] = '3.18.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

def querydslVersion = "7.1"
def jjwtVersion = "0.11.5"


dependencies {
    // implementation — spring boot starters
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"

    // implementation — libraries
    implementation "io.github.openfeign.querydsl:querydsl-core:${querydslVersion}"
    implementation "io.github.openfeign.querydsl:querydsl-jpa:${querydslVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14"

    implementation platform("org.testcontainers:testcontainers-bom:2.0.3")

    // compileOnly
    compileOnly "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    compileOnly "org.projectlombok:lombok"
    compileOnly "org.springframework.boot:spring-boot-devtools"

    // runtimeOnly
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}", "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"
    runtimeOnly "com.mysql:mysql-connector-j"

    // annotationProcessor
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${querydslVersion}:jpa"
    annotationProcessor "org.projectlombok:lombok"

    // testImplementation
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mysql"
    testImplementation "com.redis:testcontainers-redis:2.2.4"

    // testCompileOnly
    testCompileOnly "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    testCompileOnly "org.projectlombok:lombok"

    // testAnnotationProcessor
    testAnnotationProcessor "org.projectlombok:lombok"
}

jib {
    from {
        image = "amazoncorretto:17.0.5-alpine"
    }
    to {
        tags = ["$project.version".toString(), "latest"]

    }
    container {
        creationTime = "USE_CURRENT_TIMESTAMP"
    }
}

spotless {
    // Performance optimization: Check only changed files relative to origin/main
    ratchetFrom 'origin/main'

    java {
        googleJavaFormat().aosp().reflowLongStrings()

        // 1. Enforce newline after field annotations (Complementing Google Style)
        replaceRegex 'Field annotations on new line', '(?m)^([ \\t]*)(@[\\w\\.]+(?:\\([^)]*\\))?)[ \\t]+(private|protected|public|final|static)', '$1$2\n$1$3'

        // 2. Remove blank line at the start of a block (Maintain consistency)
        replaceRegex 'No blank line at start of block', '(\\{)\\n\\s*\\n', '$1\n'

        // 3. Import ordering (Java standard -> External libraries -> Project code -> Static imports)
        // Empty string '' represents all other packages not explicitly defined (external libraries)
        // '\#' represents static imports
        importOrder('java', 'javax', 'jakarta', '', 'me.jaeyeop', '\\#')

        // 4. Allow disabling formatting for specific sections (// spotless:off ... // spotless:on)
        toggleOffOn()

        // 5. Common formatting
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jvmArgs '-XX:+EnableDynamicAgentLoading'
    testLogging { events "passed", "skipped", "failed" }
}

def setupTestPath = {
    testClassesDirs = tasks.test.testClassesDirs
    classpath = tasks.test.classpath
}

tasks.register("unitTest", Test) {
    useJUnitPlatform { includeTags "unit" }
    configure setupTestPath
}

tasks.register("integrationTest", Test) {
    useJUnitPlatform { includeTags "integration" }
    configure setupTestPath
}
