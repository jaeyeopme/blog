plugins {
    id "java"
    id "org.springframework.boot" version "3.5.8"
    id "io.spring.dependency-management" version "1.1.7"
    id "com.google.cloud.tools.jib" version "3.5.1"
    id "com.diffplug.spotless" version "8.1.0"
}

group = "me.jaeyeop"
version = "1.0.0"

// spring boot dependency management 에서
// 구성되는 commons-lang3 버전을 고정(3.17.0 이하 보안 취약점)
ext['commons-lang3.version'] = '3.18.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

def querydslVersion = "7.1"
def jjwtVersion = "0.11.5"
def testcontainersVersion = "1.17.4"


dependencies {
    // implementation — spring boot starters
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"

    // implementation — libraries
    implementation "io.github.openfeign.querydsl:querydsl-core:${querydslVersion}"
    implementation "io.github.openfeign.querydsl:querydsl-jpa:${querydslVersion}"
    implementation "org.mapstruct:mapstruct:1.5.3.Final"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14"

    // compileOnly
    compileOnly "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    compileOnly "org.projectlombok:lombok"
    compileOnly "org.springframework.boot:spring-boot-devtools"

    // runtimeOnly
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}", "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"
    runtimeOnly "org.mariadb.jdbc:mariadb-java-client"

    // annotationProcessor
    annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:${querydslVersion}:jpa"
    annotationProcessor "org.mapstruct:mapstruct-processor:1.5.3.Final"
    annotationProcessor "org.projectlombok:lombok"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"

    // testImplementation
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:mariadb:${testcontainersVersion}"
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"

    // testCompileOnly
    testCompileOnly "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
    testCompileOnly "org.projectlombok:lombok"

    // testAnnotationProcessor
    testAnnotationProcessor 'org.projectlombok:lombok'
}

jib {
    from {
        image = "amazoncorretto:17.0.5-alpine"
    }
    to {
        tags = ["$project.version".toString(), "latest"]

    }
    container {
        creationTime = "USE_CURRENT_TIMESTAMP"
    }
}

spotless {
    java {
        googleJavaFormat().aosp().reflowLongStrings()
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    jvmArgs '-XX:+EnableDynamicAgentLoading'

    testLogging { events "passed", "skipped", "failed" }

    outputs.upToDateWhen { false }
}

def setupTestPath = {
    testClassesDirs = tasks.test.testClassesDirs
    classpath = tasks.test.classpath
}

tasks.register("unitTest", Test) {
    useJUnitPlatform { includeTags "unit" }
    configure setupTestPath
}

tasks.register("integrationTest", Test) {
    useJUnitPlatform { includeTags "integration" }
    configure setupTestPath
}
